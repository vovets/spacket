function(gt_debug)
  foreach(message_ ${ARGV})
    if(GATHER_DEBUG)
      message(STATUS "[GATHER_DEBUG] ${message_}")
    endif()
  endforeach()
endfunction()

function(gt_add_sources)
  set(paths_ ${ARGV})
  foreach(x_ ${paths_})
    if(IS_ABSOLUTE ${x_})
      list(APPEND srcs_ "${x_}")
    else()
      list(APPEND srcs_ "${CMAKE_CURRENT_LIST_DIR}/${x_}")
    endif()
  endforeach()
  set(result ${srcs_} PARENT_SCOPE)
endfunction()

function(gt_include  srcs_var_name dir)
  if(IS_ABSOLUTE "${dir}/sources.cmake")
    set(path_ "${dir}/sources.cmake")
  else()
    set(path_ "${CMAKE_CURRENT_LIST_DIR}/${dir}/sources.cmake")
  endif()
  include("${path_}")
  gt_debug("included ${path_}")
  set(${srcs_var_name} ${result} PARENT_SCOPE)
  gt_debug("set ${srcs_var_name} to ${result}")
endfunction()
